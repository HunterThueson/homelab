# ./home/standard-config.nix
#
# configuration options to be applied to all users by default
# 

{ config, pkgs, inputs, ... }:

{

# Must be declared per user and changed for each new version of NixOS (per guy on Discord)
home.stateVersion = "25.11";

# Let home-manager install and manage itself
programs.home-manager.enable = true;

# Extra directories to add to PATH
home.sessionPath = [
  "/usr/local/bin/"
  "$HOME/bin/"
  "$HOME/.cargo/bin/"
];

############################
## Keyboard Configuration ##
############################

home.keyboard = {
  layout = "us";
  options = [ "ctrl:nocaps" ];
};

#########################
## Shell Configuration ##
#########################

#  Note: `bash` must be enabled per-user in their respective [user].nix file in
#  order to allow each user to use a different shell (such as `fish` or `zsh`):
#
#   programs.bash.enable = true;
#

programs.bash = {
  historyFile = "$HOME/docs/archive/.bash_history";                             # Location of the bash history file
  historyIgnore = [                                                             # List of commands that should not be saved to the history list
    "ls"
    "exa"
    "cd"
    "exit"
    "clear"
  ];
  sessionVariables = rec {                                                      # Environment variables that will be set for the Bash session
    XDG_CONFIG_HOME = "$HOME/.config";
    XDG_CACHE_HOME = "$HOME/.cache";
    STARSHIP_CONFIG = "${XDG_CONFIG_HOME}/starship.toml";
    STARSHIP_CACHE = "${XDG_CACHE_HOME}/starship";
  };

  ###################
  ## Shell Options ##
  ###################

  shellOptions = [
    # Append to history file rather than replacing it
    "histappend"

    # Check the window size after each command and, if necessary, update the values of LINES and COLUMNS.
    "checkwinsize"

    # Extended globbing
    "extglob"
    "globstar"

    # Warn if closing shell with running jobs
    "checkjobs"
  ];

  ###############
  ## ~/.bashrc ##
  ###############

  bashrcExtra = ''
    # Teleport to $HOME
    cdd () {
      cd $HOME
      clear
      sleep 0.01
      eza -DG --icons=auto --group-directories-first
    }

    # Teleport to config directory
    cdc () {
      cd $XDG_CONFIG_HOME
      clear
      eza -DG --icons=auto --group-directories-first
    }

    # `gh` wrapper to make listing issues easier
    gh () {
      if [[ $@ == "issue list" ]]; then
        command gh issue list -L 100
      else
        command gh "$@"
      fi
    }

  '';

  #####################
  ## ~/.bash_aliases ##
  #####################

  shellAliases = {

    # `ls` quality-of-life improvements
    ls = "eza -G --color=auto --icons=auto --group-directories-first";          # replace `ls` with `eza` (faster, written in Rust)
    lsa = "eza -Gau --git --time-style long-iso --color=always --icons";        # more info about hidden files (w/ git status)
    lsd = "eza -D --color=auto --icons=auto";                                   # list only directories
    lst = "eza -T --color=auto --icons=auto --group-directories-last";          # list recursively through directories and output as a tree

    # Drop-in program replacements
    find = "fd";                                                                # replace `find` with `fd` (faster, written in -- you guessed it -- Rust)
    
    # Navigation
    ghs = "git status";
    
    # Terminal clearing
    cl = "clear";                                                               # because typing `clear` just takes too long
  };

};

# Enable Starship prompt for all users
programs.starship = {
  enable = true;
  enableBashIntegration = true;
};
  
programs.alacritty = {
  enable = true;
};

programs.firefox = {
  enable = true;
};

}

